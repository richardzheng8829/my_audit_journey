# No slippage protection for `Fees.sellProfit()`

High

https://github.com/Cyfrin/2023-07-beedle/blob/main/src/Fees.sol#L38
## Summary
Fees.sellProfits() exchanges profits generated into WETH, then transfer it into `staking` address.
But it does not specify how much slippage can be tolerated.

## Vulnerability Details
The swap is called with amountOutMinimum: 0, meaning that there is no slippage protection in this swap. This could result in a significant loss of yield from this reward as MEV bots could “sandwich” this swap by manipulating the price before this transaction and immediately reversing their action after the transaction, profiting at the expense of our swap. 

```solidity
    function sellProfits(address _profits) public {
        require(_profits != WETH, "not allowed");
        uint256 amount = IERC20(_profits).balanceOf(address(this));

        ISwapRouter.ExactInputSingleParams memory params = ISwapRouter
            .ExactInputSingleParams({
                tokenIn: _profits,
                tokenOut: WETH,
                fee: 3000,
                recipient: address(this),
                deadline: block.timestamp,
                amountIn: amount,
                amountOutMinimum: 0, // @audit - no slippage protection
                sqrtPriceLimitX96: 0
            });

        amount = swapRouter.exactInputSingle(params);
        IERC20(WETH).transfer(staking, IERC20(WETH).balanceOf(address(this)));
    }
```
## Impact
It may lose some of the profits generated to MEV bots.

## Tools Used
Manual Review

## Recommendations
Fees.sellProfits() should include slippage percentage allowed.

```solidity
-    function sellProfits(address _profits) public {
+    function sellProfits(address _profits, uint256 amountOutMinimum) public {
        require(_profits != WETH, "not allowed");
        uint256 amount = IERC20(_profits).balanceOf(address(this));

        ISwapRouter.ExactInputSingleParams memory params = ISwapRouter
            .ExactInputSingleParams({
                tokenIn: _profits,
                tokenOut: WETH,
                fee: 3000,
                recipient: address(this),
                deadline: block.timestamp,
                amountIn: amount,
-                amountOutMinimum: 0, // @audit - no slippage protection
+                amountOutMinimum: amountOutMinimum,
                sqrtPriceLimitX96: 0
            });

        amount = swapRouter.exactInputSingle(params);
        IERC20(WETH).transfer(staking, IERC20(WETH).balanceOf(address(this)));
    }
```