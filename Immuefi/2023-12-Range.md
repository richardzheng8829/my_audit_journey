# Token balance in `RangeProtocolVault.userVaults` mapping will be incorrect after `RangeProtocolVault.swap()`.

## Bug Description
`RangeProtocolVault` keeps track of every user's token0, token1 balance by updating `userVaults` mapping while minting, burning, transfering vault token.

But when swapping between token0 and token1, it does not update userVaults. it will cause wrong accounting issue.

https://github.com/Range-Protocol/contracts/blob/base-uniswap-deployment/contracts/RangeProtocolVaultStorage.sol#L27-L32
```
    struct UserVault {
        bool exists;
        uint256 token0;
        uint256 token1;
    }
    mapping(address => UserVault) public userVaults;
``` 

https://github.com/Range-Protocol/contracts/blob/base-uniswap-deployment/contracts/RangeProtocolVault.sol#L212-L219
```
        if (amount0 > 0) {
            userVaults[msg.sender].token0 += amount0;
            token0.safeTransferFrom(msg.sender, address(this), amount0);
        }
        if (amount1 > 0) {
            userVaults[msg.sender].token1 += amount1;
            token1.safeTransferFrom(msg.sender, address(this), amount1);
        }
```

https://github.com/Range-Protocol/contracts/blob/base-uniswap-deployment/contracts/RangeProtocolVault.sol#L288-L296
```
        userVaults[msg.sender].token0 =
            (userVaults[msg.sender].token0 * (vars.balanceBefore - burnAmount)) /
            vars.balanceBefore;
        if (amount0 > 0) token0.safeTransfer(msg.sender, amount0);

        userVaults[msg.sender].token1 =
            (userVaults[msg.sender].token1 * (vars.balanceBefore - burnAmount)) /
            vars.balanceBefore;
        if (amount1 > 0) token1.safeTransfer(msg.sender, amount1);
```

https://github.com/Range-Protocol/contracts/blob/base-uniswap-deployment/contracts/RangeProtocolVault.sol#L648-L661
```
        uint256 senderBalance = balanceOf(from);
        uint256 token0Amount = userVaults[from].token0 -
            (userVaults[from].token0 * (senderBalance - amount)) /
            senderBalance;

        uint256 token1Amount = userVaults[from].token1 -
            (userVaults[from].token1 * (senderBalance - amount)) /
            senderBalance;

        userVaults[from].token0 -= token0Amount;
        userVaults[from].token1 -= token1Amount;

        userVaults[to].token0 += token0Amount;
        userVaults[to].token1 += token1Amount;
```

## Impact
Token balance in `userVaults` mapping will contain incorrect balances.

## Recommendation
Can create following view function instead of userVaults mapping to get user's token balance.

It will save gas too, as there's no need to update mapping during minting, burning, transfering vault token.

```
    function getUserTokenBalance(address user) external view returns (uint256 amount0, uint256 amount1) {
        (amount0, amount1) = getUnderlyingBalances();
        amount0 = amount0 * balanceOf(user) / totalSuppy();
        amount0 = amount1 * balanceOf(user) / totalSuppy();
    }
```

## Proof of Concept
```
pragma solidity 0.8.20;

import "forge-std/Test.sol";

import "@openzeppelin/contracts/interfaces/IERC20.sol";
import "../src/interfaces/IUniswapPairV2.sol";

interface IRangeProtocolVault {
    function mint(
        uint256 mintAmount
    ) external returns (uint256 amount0, uint256 amount1);

    function burn(
        uint256 burnAmount
    ) external returns (uint256 amount0, uint256 amount1);

    function swap(
        bool zeroForOne,
        int256 swapAmount,
        uint160 sqrtPriceLimitX96
    ) external returns (int256 amount0, int256 amount1);

    function userVaults(address user) external view returns (
        bool exists,
        uint256 token0,
        uint256 token1);

    function manager() external view returns (address);
}

IRangeProtocolVault constant vault = IRangeProtocolVault(0x9Ad8d0df2dA118DcE898b7F5BD9Ab749c593A5d9);
IERC20 constant WETH = IERC20(0xC02aaA39b223FE8D0A0e5C4F27eAD9083C756Cc2);
IERC20 constant USDC = IERC20(0xA0b86991c6218b36c1d19D4a2e9Eb0cE3606eB48);

contract RangeTest is Test {

    uint160 internal constant MIN_SQRT_RATIO = 4295128739;
    uint160 internal constant MAX_SQRT_RATIO = 1461446703485210103287273052203988822378723970342;

    function setUp() public {
        vm.createSelectFork("mainnet");

        vm.label(address(vault), "Vault");
        vm.label(address(WETH), "WETH");
        vm.label(address(USDC), "USDC");
    }
    
    function testRange() public {
        uint256 token0;
        uint256 token1;

        console.log("Step 1: Minting ...");
        deal(address(USDC), address(this), 1 ether);
        deal(address(WETH), address(this), 1000 ether);
        USDC.approve(address(vault), type(uint256).max);
        WETH.approve(address(vault), type(uint256).max);
        vault.mint(100000);
        (, token0, token1) = vault.userVaults(address(this));
        emit log_named_decimal_uint("USDC", token1, 6);
        emit log_named_decimal_uint("WETH", token0, 18);

        console.log("Step 2: Swapping ...");
        vm.startPrank(vault.manager());
        vault.swap(true, 100000, MIN_SQRT_RATIO + 1);
        vm.stopPrank();
        (, token0, token1) = vault.userVaults(address(this));

        vault.burn(100000);
        emit log_named_decimal_uint("USDC", token1, 6);
        emit log_named_decimal_uint("WETH", token0, 18);
    }
}
```