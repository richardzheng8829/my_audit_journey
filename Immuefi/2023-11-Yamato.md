# Clearing approval of CJPY token will revert. Possible loss of fund and incompatibility with other DeFi protocols.

## Bug Description
CJPY token will revert when apporval amount is zero.

https://github.com/yamatoprotocol/core/blob/master/contracts/Currency.sol#L52-L64
```
    function approve(
        address spender,
        uint256 amount
    ) public override returns (bool) {
        require(
            _msgSender() != spender,
            "sender and spender shouldn't be the same."
        );
        require(amount > 0, "Amount is zero."); //@audit - No way to clear approval

        _approve(_msgSender(), spender, amount);
        return true;
    }
```

## Impact

1) As users cannot clear approval after giving wrong approval, it might lead to loss of fund.

2) Another complication is that incompatibility with DeFi protocols.

CJPY token might be integrated with other DeFi protocols to be used as a collateral for example.

The most widely used way to reset approval in DeFi protocols is to clear approval and re-approve with amount.

https://github.com/aave/protocol-v2/blob/master/contracts/adapters/BaseUniswapAdapter.sol#L153-L155
```
    // Approves the transfer for the swap. Approves for 0 first to comply with tokens that implement the anti frontrunning approval fix.
    IERC20(assetToSwapFrom).safeApprove(address(UNISWAP_ROUTER), 0);
    IERC20(assetToSwapFrom).safeApprove(address(UNISWAP_ROUTER), amountToSwap);
```

And Here's OpenZeppelin's safeApprove() function.

https://github.com/OpenZeppelin/openzeppelin-contracts/blob/v4.9.2/contracts/token/ERC20/utils/SafeERC20.sol#L45-L54
```
    function safeApprove(IERC20 token, address spender, uint256 value) internal {
        // safeApprove should only be called when setting an initial allowance,
        // or when resetting it to zero. To increase and decrease it, use
        // 'safeIncreaseAllowance' and 'safeDecreaseAllowance'
        require(
            (value == 0) || (token.allowance(address(this), spender) == 0),
            "SafeERC20: approve from non-zero to non-zero allowance"
        );
        _callOptionalReturn(token, abi.encodeWithSelector(token.approve.selector, spender, value));
    }
```

Even if they reset to dust amount, for example 1 wei. `SafeERC20.safeApprove()` will revert instead.

## Recommendation
Please remove check if approval amount is greater than zero.

https://github.com/yamatoprotocol/core/blob/master/contracts/Currency.sol#L52-L64
```
    function approve(
        address spender,
        uint256 amount
    ) public override returns (bool) {
        require(
            _msgSender() != spender,
            "sender and spender shouldn't be the same."
        );
-       require(amount > 0, "Amount is zero."); //@audit - No way to clear approval

        _approve(_msgSender(), spender, amount);
        return true;
    }
```

## Proof of Concept
```
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.10;

import "forge-std/Test.sol";
import "@openzeppelin/contracts/interfaces/IERC20Metadata.sol";
import "@openzeppelin/contracts/token/ERC20/utils/SafeERC20.sol";

contract ContractTest is Test {
  IERC20Metadata    constant CJPY = IERC20Metadata(0x1cfa5641c01406aB8AC350dEd7d735ec41298372);
  address           constant target = address(0x01);

  using SafeERC20 for IERC20Metadata;

  function setUp() public {
    vm.createSelectFork("mainnet"); 

    deal(address(CJPY), address(this), 100 ether);
    vm.expectRevert();
    CJPY.approve(target, 0);
  }

  function testCJPY_ClearApproval() public {
    console.log("Clearing approval will revert");
    CJPY.approve(target, 0);
  }

  function testCJPY_Reapprove_No_Dust() public {
    console.log("Re-approval with no dust amount will revert!!!");
    reapprove(50 ether, false);
  }

  function testCJPY_Reapprove_Dust() public {
    console.log("Re-approval with dust amount will revert!!!");
    reapprove(50 ether, true);
  }

  function reapprove(uint256 allowance, bool useDustAmount) internal {
    CJPY.safeApprove(target, useDustAmount ? 1 : 0);
    CJPY.safeApprove(target, allowance);
  }
}
```

### Output
forge test --match-test CJPY -vvv
```
[FAIL. Reason: Amount is zero.] testCJPY_ClearApproval() (gas: 6709)
Logs:
  Clearing approval will revert

[FAIL. Reason: SafeERC20: approve from non-zero to non-zero allowance] testCJPY_Reapprove_Dust() (gas: 34745)
Logs:
  Re-approval with dust amount will revert!!!

[FAIL. Reason: Amount is zero.] testCJPY_Reapprove_No_Dust() (gas: 7808)
Logs:
  Re-approval with no dust amount will revert!!!

Test result: FAILED. 0 passed; 3 failed; 0 skipped; finished in 8.56s
Ran 1 test suites: 0 tests passed, 3 failed, 0 skipped (3 total tests)
```