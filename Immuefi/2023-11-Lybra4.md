# Price oracle for LBR, eUSD, LBR-WETH LP return zero as last updated time, consumer of price feed does not check for price staleness.

## Bug Description
`latestRoundData()` of LBR, eUSD, LP price oracle which are centralized updated by Lybra Finance return zero as last updated time.

http://etherscan.io/address/0x1932d36f5Dd86327CEacd470271709a931803338
```
contract LBRPriceOFeed {
    function latestRoundData() external view returns(uint80, int, uint, uint, uint80) {
        uint256 price = pairOracle.getAssetPrice(LBR);
        ( ,int256 ethPrice, , , timestamp) = priceFeed.latestRoundData();
        uint256 lbrPrice = price * uint256(ethPrice) / 1e18;
        return (0, int(lbrPrice), 0,0,0);
    }
}
```

`LybraConfigurator`, `ProtocolRewardsPool`, `EUSDMiningIncentive`, `CollateralRatioGuardian`  rely on oracle to fetch prices of LBR, eUSD, LBR-WETH LP.

https://github.com/LybraFinance/LybraV2/blob/main/contracts/lybra/miner/EUSDMiningIncentives.sol#L189-L192
```
    function stakedLBRLpValue(address user) public view returns (uint256) {
        (, int lpPriceFeed, , , ) = lpPriceFeed.latestRoundData();
        return IEUSD(ethlbrStakePool).balanceOf(user) * uint256(lpPriceFeed) / 1e8;
    }

    function _buyOtherEarnings(address user, bool useEUSD) internal updateReward(user) {
        require(isOtherEarningsClaimable(user), "The rewards of the user cannot be bought out");
        require(rewards[user] != 0, "ZA");
        if(useEUSD) {
            require(isEUSDBuyoutAllowed, "The purchase using eUSD is not permitted.");
        }
        uint256 reward = rewards[user];
        rewards[user] = 0;
        uint256 biddingFee = (reward * biddingFeeRatio) / 10_000;
        if(useEUSD) {
            (, int lbrPrice, , , ) = lbrPriceFeed.latestRoundData();
            biddingFee = biddingFee * uint256(lbrPrice) / 1e8;
            bool success = EUSD.transferFrom(msg.sender, address(owner()), biddingFee);
            require(success, "TF");
        } else {
            IesLBR(LBR).burn(msg.sender, biddingFee);
        }
        IesLBR(esLBR).mint(msg.sender, reward);
        emit ClaimedOtherEarnings(msg.sender, user, reward, biddingFee, useEUSD, block.timestamp);
    }
```

Oracle staleness is situation which might occur for some time.

For example, at 2023/11/30/03:43 UTC+0,, current market rate of LBR at Uniswap, Binance, OKEX is around $1.08, but LBR oracle indicates $1.15.

https://etherscan.io/address/0x1932d36f5Dd86327CEacd470271709a931803338#readContract
```
    115316735
```

It's because oracle operator could not update oracle price since last 2 days.

https://etherscan.io/address/0xc626867660Df1cc6274ce0338E7DfB92474647C5

As there might be several reasons for staleness, one should assume that price can always be stale for a long time.
- Lack of gas fund.
- Off-chain bot got crushed because of error.

## Impact
As they all lack staleness check, it may cause several issues like (except ether price oracle operated by Liquity)

- Unfair discounted buying of other user's earning.
- Wrong price of eUSD at discounted buying of LBR.
- Wrong eUSD price at premium trading while distributing reward.

## Recommendation
1) Price oracle return should have last good price timestamp.
2) User of price oracle should check if price is stale or not.

http://etherscan.io/address/0x1932d36f5Dd86327CEacd470271709a931803338
```
contract LBRPriceOFeed {
    function latestRoundData() external view returns(uint80, int, uint, uint, uint80) {
        uint256 price = pairOracle.getAssetPrice(LBR);
        ( ,int256 ethPrice, , , timestamp) = priceFeed.latestRoundData();
        uint256 lbrPrice = price * uint256(ethPrice) / 1e18;
-        return (0, int(lbrPrice), 0,0,0);
+        return (0, int(lbrPrice), 0,pairOracle.blockTimestampLast(),0);
    }
}
```
```
    function stakedLBRLpValue(address user) public view returns (uint256) {
-       (, int lpPriceFeed, , , ) = lpPriceFeed.latestRoundData();
+       (, int lpPriceFeed, , uint256 updatedAt, ) = lpPriceFeed.latestRoundData();
+       require (block.timestamp > updatedAt + MAX_DELAY, "Stale price");
        return IEUSD(ethlbrStakePool).balanceOf(user) * uint256(lpPriceFeed) / 1e8;
    }
```

## Proof of Concept
```
pragma solidity 0.8.20;

import "forge-std/Test.sol";

interface AggregatorV3Interface {
    function latestRoundData()
        external
        view
        returns (
            uint80 roundId,
            int256 answer,
            uint256 startedAt,
            uint256 updatedAt,
            uint80 answeredInRound
        );
}

contract ContractTest is Test {
    AggregatorV3Interface  lbrPriceFeed = AggregatorV3Interface(0x1932d36f5Dd86327CEacd470271709a931803338);

    function setUp() public {
        vm.createSelectFork("mainnet", 18682004);
    }
    
    function testOracle() public {
        uint256 updatedAt;
        (, , , updatedAt, ) = lbrPriceFeed.latestRoundData();
        console.log("Last update time of LBR price feed", updatedAt);
    }
}
```

### Output
```
[PASS] testOracle() (gas: 35838)
Logs:
  Last update time of LBR price feed 0
```