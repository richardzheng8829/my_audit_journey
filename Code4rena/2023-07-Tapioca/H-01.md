## Summary

`BigBang.sol._borrow()` does not collect borrowing fee. It does not mint enough USDO token including borrowing fee.

## Severity

High

## Proof of Concept

BigBang's borrow opening fee is 0.05%.

https://github.com/Tapioca-DAO/tapioca-bar-audit/blob/master/contracts/markets/bigBang/BigBang.sol#L167
```solidity
        borrowOpeningFee = 50; // 0.05%
```


In order to collect borrowing fee, `amount + feeAmount` USDO stablecoin should be minted, and `feeAmount` should be sent to protocol treasury which is `Penrose.sol` by increasing `totalFee` variable.

But `_borrow()` mints only `amount`, thus there's nothing left to collect as fee.

https://github.com/Tapioca-DAO/tapioca-bar-audit/blob/master/contracts/markets/bigBang/BigBang.sol#L742-L767
```solidity
function _borrow(
        address from,
        address to,
        uint256 amount
    ) internal returns (uint256 part, uint256 share) {
        uint256 feeAmount = (amount * borrowOpeningFee) / FEE_PRECISION; // A flat % fee is charged for any borrow

        (totalBorrow, part) = totalBorrow.add(amount + feeAmount, true);
        require(
            totalBorrowCap == 0 || totalBorrow.elastic <= totalBorrowCap,
            "BigBang: borrow cap reached"
        );

        userBorrowPart[from] += part;

        //mint USDO
        IUSDOBase(address(asset)).mint(address(this), amount); //@audit -wrong fee collection - fee amount is not minted. nothing to collect

        //deposit borrowed amount to user
        asset.approve(address(yieldBox), amount);
        yieldBox.depositAsset(assetId, address(this), to, amount, 0);

        share = yieldBox.toShare(assetId, amount, false);

        emit LogBorrow(from, to, amount, feeAmount, part);
    }
```

## Impact
Protocol will collect USDO borrowing fee as revenue. It will affect price of twTAP.

## Tools Used
Manual Review

## Recommendation

1) Should mint `amount + feeAmount` USDO token and send `feeAmount` to Penrose treasury.
2) Should collect `feeAmount` as fee

```solidity
function _borrow(
        address from,
        address to,
        uint256 amount
    ) internal returns (uint256 part, uint256 share) {
        uint256 feeAmount = (amount * borrowOpeningFee) / FEE_PRECISION; // A flat % fee is charged for any borrow

        (totalBorrow, part) = totalBorrow.add(amount + feeAmount, true);
        require(
            totalBorrowCap == 0 || totalBorrow.elastic <= totalBorrowCap,
            "BigBang: borrow cap reached"
        );

        userBorrowPart[from] += part;

        //mint USDO
-        IUSDOBase(address(asset)).mint(address(this), amount); // @audit -wrong fee collection - fee amount is not minted. nothing to collect
+        IUSDOBase(address(asset)).mint(address(this), amount + feeAmount); 
+       totalFees += feeAmount;

        //deposit borrowed amount to user
        asset.approve(address(yieldBox), amount);
        yieldBox.depositAsset(assetId, address(this), to, amount, 0);

        share = yieldBox.toShare(assetId, amount, false);

        emit LogBorrow(from, to, amount, feeAmount, part);
    }
```
